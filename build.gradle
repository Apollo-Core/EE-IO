

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
// for compliance with higher Java versions
jacoco {
  toolVersion = "0.8.5"
}
// to use GitHub repositories as dependencies

group 'Apollo-Core-EE-IO'
version '1.0-SNAPSHOT'


repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
}

dependencies {

  // dependencies to other parts of the Apollo Core EE
  if (project.hasProperty("local")) {
    implementation project (':EE-Core')
    implementation project (':EE-Guice')
    implementation project (':EE-Model')
  }else {
    implementation 'com.github.Apollo-Core:EE-Core:master-SNAPSHOT'
    implementation 'com.github.Apollo-Core:EE-Guice:main-SNAPSHOT'
    implementation 'com.github.Apollo-Core:EE-Model:main-SNAPSHOT'
  }

  // dependencies to projects from other parts of Apollo
  implementation 'com.github.Apollo-AFCL:AFCLCore-AFCLv1.1:master-SNAPSHOT'

  // SLF4J and Logback for logging
  implementation group: "ch.qos.logback", name: "logback-classic", version: "1.2.3"

  // gson to define the format of the exchanged messages
  implementation 'com.google.code.gson:gson:2.8.6'

  // vertx
  implementation 'io.vertx:vertx-core:4.1.0'

  // dependency to Opt4J
  implementation 'com.github.FedorSmirnov89:opt4j:master-SNAPSHOT'
  // dependency to opendse
  implementation 'com.github.FedorSmirnov89:opendse:master-SNAPSHOT'

  // test dependencies
  testImplementation(platform('org.junit:junit-bom:5.7.2'))
  testImplementation('org.junit.jupiter:junit-jupiter')
  testImplementation group: 'org.mockito',     name: 'mockito-core',     version: '3.4.4'

}

java {
  withSourcesJar()
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
    }
  }
}

if (rootProject == project) {
  wrapper {
    gradleVersion = "6.8.3"
    distributionType = Wrapper.DistributionType.ALL
  }
}

test {
  useJUnitPlatform()
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
  afterEvaluate {
    classDirectories.from = files(classDirectories.files.collect {
      fileTree(dir: it, exclude: '**/modules/**')
    })
  }
}